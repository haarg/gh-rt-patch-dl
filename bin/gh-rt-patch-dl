#!/usr/bin/env perl
use strict;
use warnings;
use Getopt::Long qw(:config gnu_getopt);
use Pod::Usage;
use JSON::PP qw(decode_json);
use HTTP::Tiny;

GetOptions(
  'h|help'  => \my $help,
  'rt'      => \my $rt,
  'gh'      => \my $gh,
) or Pod::Usage::pod2usage(1);

if ($help) {
  Pod::Usage::pod2usage(
    -exitval => 0,
    -verbose => 2,
  );
}

if ($rt && $gh) {
  die "--rt and --gh options are not compatible";
}

$gh = 1
  if !$rt;

my $rt_ua = HTTP::Tiny->new(max_redirect => 0, timeout => 10);
my @issues;
for my $arg (@ARGV) {
  if (
    $arg =~ m{\Ahttps?://rt\.perl\.org/.*\.html\?id=([0-9]+)(?:;|&|\z)}
    or $arg =~ /\Art#?([0-9]+)\z/i
    or $rt && $arg =~ /\A#?([0-9]+)\z/
  ) {
    my $rt_ticket = $1;
    my $res = $rt_ua->get("https://rt.perl.org/Public/Bug/Display.html?id=$rt_ticket");
    if (my $loc = $res->{headers}{location}) {
      if ($loc =~ m{\Ahttps?://github\.com/Perl/perl/issues/([0-9]+)\z}i) {
        push @issues, $1;
        next;
      }
    }
    warn "Unable to find GitHub issue for RT#$rt_ticket\n";
  }
  elsif (
    $arg =~ m{\Agh#?([0-9]+)(?:/([0-9]+))?\z}i
    or $arg =~ m{\Ahttps://github\.com/Perl/perl5/issues/(\d+)(?:#issuecomment-([0-9]+))?\z}i
    or $gh && $arg =~ m{\A#?([0-9])(?:/([0-9]+))?\z}
  ) {
    my $issue = $1;
    $issue .= "/$2"
      if defined $2;
    push @issues, $issue;
  }
  else {
    die "$arg does not look like an issue\n";
  }
}

my $ua = HTTP::Tiny->new;
sub gh_req {
  my $url = shift;
  my $res = $ua->get("https://api.github.com/$url");
  if (!$res->{success}) {
    if ($res->{status} == 403) {
      die "Hit GitHub rate limit!\n";
    }
    die "Error querying GitHub:\n$res->{content}";
  }
  return decode_json($res->{content});
}

for my $issue (@issues) {
  ($issue, my $comment) = split m{/}, $issue;
  print "Issue $issue" . ($comment ? " comment $comment" : '') . ": https://github.com/Perl/perl5/issues/$issue" . ($comment ? "#issuecomment-$comment" : "") . "\n";

  my $comments_data;
  if ($comment) {
    $comments_data = [
      gh_req("repos/Perl/perl5/issues/comments/$comment"),
    ];
  }
  else {
    $comments_data = gh_req("repos/Perl/perl5/issues/$issue/comments");
  }

  my $found = 0;
  for my $comment (@$comments_data) {
    my $body = $comment->{body};
    while ($body =~ m{
      <details><summary>([^<]+)</summary>\n
      \n
      ```diff\n
      (.+?)
      \n```\n
      </details>\n
    }sgx) {
      my ($file, $patch) = ($1, $2);
      print "    Patch: $file\n";
      $file =~ s{/}{-}g;
      my $write_file = $file;
      my $post = 0;
      while (-e $write_file) {
        $write_file = $file . '.' . ++$post;
      }
      print "    Writing $write_file\n";
      open my $fh, '>:utf8', $write_file
        or die "Can't write to $write_file: $!\n";
      print $fh $patch;
      close $fh;
      $found++;
    }
  }
  print "Found $found patch".($found==1 ? '' : 'es').".\n";
}

__END__

=head1 NAME

gh-rt-patch-dl - Download patches migrated from Perl RT to GitHub

=head1 SYNOPSIS

  $ gh-rt-patch-dl 17006
  $ gh-rt-patch-dl https://github.com/Perl/perl5/issues/17006
  $ gh-rt-patch-dl gh#17006

  $ gh-rt-patch-dl --rt 134118
  $ gh-rt-patch-dl rt#134118
  $ gh-rt-patch-dl https://rt.perl.org/Public/Bug/Display.html?id=

=cut
